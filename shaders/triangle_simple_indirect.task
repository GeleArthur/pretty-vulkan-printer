#version 460
#pragma shader_stage(task)
#extension GL_EXT_mesh_shader: enable
#extension GL_KHR_shader_subgroup_ballot: enable
#extension GL_EXT_buffer_reference: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types: require

#include "shared_structs.glsl"
#include "world_binds.glsl"

layout (local_size_x = AS_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

taskPayloadSharedEXT Payload payload;


layout (std430, set = 1, binding = 0) readonly buffer DrawCommandIn {
    DrawCommand Commands[];
};

layout (std430, set = 1, binding = 1) readonly buffer ModelMatrixIn {
    mat4 ModelMatrix[];
};

layout (std430, set = 1, binding = 6) readonly buffer SphereBoundsIn {
    ConeBounds coneBoundsData[];
};

void main()
{
    bool visible = false;
    if (gl_GlobalInvocationID.x >= Commands[gl_DrawID].meshlet_count) {
        visible = false;
        return; // TODO wrong
    }

    mat4 model_matrix = ModelMatrix[gl_DrawID];
    payload.model_matrix_id = gl_DrawID;

    vec3 scale = vec3(
    length(model_matrix[0].xyz),
    length(model_matrix[1].xyz),
    length(model_matrix[2].xyz)
    );
    float maxScale = max(max(scale.x, scale.y), scale.z);

    uint offset = Commands[gl_DrawID].meshlet_offset + gl_GlobalInvocationID.x;
    ConeBounds cone = coneBoundsData[offset];

    vec4 sphere_bounds = vec4(
    vec3(model_matrix * vec4(cone.sphereBounds.xyz, 1.0)),
    cone.sphereBounds.w * maxScale
    );

    visible = VisibleFrustumCone(sphere_bounds);

    if (dot(normalize(cone.sphereBounds.xyz - sceneInfo.position), cone.coneAxis.xyz) >= cone.coneAxis.w) {
        visible = false;
    }


    uvec4 ballot = subgroupBallot(visible);

    if (visible) {
        uint index = subgroupBallotExclusiveBitCount(ballot);
        payload.MeshletIndices[index] = Commands[gl_DrawID].meshlet_offset + gl_GlobalInvocationID.x;
        payload.visable[index] = visible;
    }

    uint visible_count = subgroupBallotBitCount(ballot);

    EmitMeshTasksEXT(visible_count, 1, 1);
}
