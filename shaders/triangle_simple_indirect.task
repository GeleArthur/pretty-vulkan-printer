#version 460
#pragma shader_stage(task)
#extension GL_EXT_mesh_shader: enable
#extension GL_KHR_shader_subgroup_ballot: enable

#define AS_GROUP_SIZE 32
layout (local_size_x = AS_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

struct FrustumCone
{
    vec3 Tip;
    float Height;
    vec3 Direction;
    float Angle;
};

layout (set = 0, binding = 0) uniform SceneGlobals {
    mat4x4 camera_view;
    mat4x4 camera_projection;
    vec3 position;
    FrustumCone camera_frustom;
} sceneInfo;


struct Payload {
    uint MeshletIndices[AS_GROUP_SIZE];
    bool visable[AS_GROUP_SIZE];
    uint model_matrix_id;
};

taskPayloadSharedEXT Payload payload;

//layout (std140, set = 1, binding = 0) readonly buffer VertexIn {
//    Vertex Vertices[];
//};

struct DrawCommand
{
    uint groupCountX;
    uint groupCountY;
    uint groupCountZ;
    uint sphere_bounds_offset;
};

layout (std430, set = 1, binding = 0) readonly buffer DrawCommandIn {
    DrawCommand Commands[];
};

layout (std430, set = 1, binding = 1) readonly buffer ModelMatrixIn {
    mat4 ModelMatrix[];
};

layout (std430, set = 1, binding = 6) readonly buffer SphereBoundsIn {
    vec4 SphereBounds[];
};


bool VisibleFrustumCone(vec4 sphere)
{
    // Cone and sphere are within intersectable range
    vec3 v0 = sphere.xyz - sceneInfo.camera_frustom.Tip;
    float d0 = dot(v0, sceneInfo.camera_frustom.Direction);
    bool i0 = (d0 <= (sceneInfo.camera_frustom.Height + sphere.w));

    //    float diff = dot(normalize(v0), normalize(sceneInfo.camera_frustom.Direction));
    //    bool inside = diff >= cos(sceneInfo.camera_frustom.Angle);

    // TODO: Find source for this code.
    float cs = cos(sceneInfo.camera_frustom.Angle);
    float sn = sin(sceneInfo.camera_frustom.Angle);
    float a = dot(v0, sceneInfo.camera_frustom.Direction);
    float b = a * sn / cs;
    float c = sqrt(dot(v0, v0) - (a * a));
    float d = c - b;
    float e = d * cs;
    bool i1 = (e < sphere.w);

    return i0 && i1;
}

void main()
{
    bool visible = false;

    mat4 model_matrix = ModelMatrix[gl_DrawID];
    payload.model_matrix_id = gl_DrawID;

    vec3 scale = vec3(
    length(model_matrix[0].xyz),
    length(model_matrix[1].xyz),
    length(model_matrix[2].xyz)
    );
    float maxScale = max(max(scale.x, scale.y), scale.z);

    uint offset = Commands[gl_DrawID].sphere_bounds_offset + gl_GlobalInvocationID.x;

    vec4 sphere_bounds = vec4(
    vec3(model_matrix * vec4(SphereBounds[offset].xyz, 1.0)),
    SphereBounds[offset].w * maxScale
    );

    visible = VisibleFrustumCone(sphere_bounds);

    uvec4 ballot = subgroupBallot(visible);

    if (visible) {
        uint index = subgroupBallotExclusiveBitCount(ballot);
        payload.MeshletIndices[index] = Commands[gl_DrawID].sphere_bounds_offset + gl_GlobalInvocationID.x;
        payload.visable[index] = visible;
    }

    uint visible_count = subgroupBallotBitCount(ballot);

    EmitMeshTasksEXT(visible_count, 1, 1);
}
