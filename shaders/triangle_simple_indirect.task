#version 460
#pragma shader_stage(task)
#extension GL_EXT_mesh_shader: enable
#extension GL_KHR_shader_subgroup_ballot: enable
#extension GL_EXT_buffer_reference: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types: require

#include "shared_structs.glsl"
#include "world_binds.glsl"

layout (local_size_x = AS_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

taskPayloadSharedEXT Payload payload;

layout (std430, set = 1, binding = 0) readonly buffer DrawCommandIn {
    DrawCommand Commands[];
};

layout (std430, set = 1, binding = 1) readonly buffer ModelMatrixIn {
    ModelInfo ModelMatrix[];
};

layout (std430, set = 1, binding = 6) readonly buffer SphereBoundsIn {
    ConeBounds coneBoundsData[];
};

void main()
{
    if (gl_GlobalInvocationID.x >= Commands[gl_DrawID].meshlet_count) {
        return;
    }

    mat4 model_matrix = ModelMatrix[gl_DrawID].model;
    payload.model_index = gl_DrawID;

    uint offset = Commands[gl_DrawID].meshlet_offset + gl_GlobalInvocationID.x;
    ConeBounds cone = TransformCone(coneBoundsData[offset], model_matrix);

    bool visible = IsVisible(cone);

    uvec4 ballot = subgroupBallot(visible);

    if (visible) {
        uint index = subgroupBallotExclusiveBitCount(ballot);
        payload.meshlet_indices[index] = Commands[gl_DrawID].meshlet_offset + gl_GlobalInvocationID.x;
        payload.visable[index] = visible;
    }

    uint visible_count = subgroupBallotBitCount(ballot);

    EmitMeshTasksEXT(visible_count, 1, 1);
}
