#version 460
#pragma shader_stage(mesh)
#extension GL_EXT_mesh_shader: enable
#extension GL_EXT_shader_8bit_storage: enable
#extension GL_EXT_shader_explicit_arithmetic_types: require
#extension GL_EXT_buffer_reference: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require


#include "shared_structs.glsl"
#include "world_binds.glsl"

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout (triangles) out;
layout (max_vertices = 64, max_primitives = 126) out;

layout (std430, set = 1, binding = 0) readonly buffer DrawCommandIn {
    DrawCommand Commands[];
};
layout (std430, set = 1, binding = 1) readonly buffer ModelMatrixIn {
    ModelInfo ModelMatrix[];
};
layout (std430, set = 1, binding = 2) readonly buffer MeshletIn {
    Meshlet Meshlets[];
};
layout (std140, set = 1, binding = 3) readonly buffer VertexIn {
    Vertex Vertices[];
};
layout (std430, set = 1, binding = 4) readonly buffer VertexIndicesIn {
    uint VertexIndices[];
};
layout (std430, set = 1, binding = 5) readonly buffer TriangleIndicesIn {
    uint8_t TriangleIndices[];
};
layout (std430, set = 1, binding = 6) readonly buffer SphereBoundsIn {
    ConeBounds SphereBounds[];
};

layout (std430, set = 1, binding = 7) readonly buffer PointersIn {
    MeshletsBuffers pointers[];
};


layout (location = 0) out vec3 vertexColor[];
layout (location = 1) out uint workGroup[];


taskPayloadSharedEXT Payload payload;

void main()
{
    uint meshletIndex = payload.meshlet_indices[gl_WorkGroupID.x];

    workGroup[gl_LocalInvocationID.x] = gl_WorkGroupID.x;

    Meshlet m = Meshlets[meshletIndex];
    mat4 model_matrix = ModelMatrix[payload.model_index].model;

    if (gl_LocalInvocationIndex == 0)
    {
        SetMeshOutputsEXT(m.vertex_count, m.triangle_count);
    }

    if (gl_LocalInvocationID.x < m.triangle_count) {
        gl_PrimitiveTriangleIndicesEXT[gl_LocalInvocationID.x] = uvec3(
        TriangleIndices[m.triangle_offset + (gl_LocalInvocationID.x * 3)],
        TriangleIndices[m.triangle_offset + (gl_LocalInvocationID.x * 3) + 1],
        TriangleIndices[m.triangle_offset + (gl_LocalInvocationID.x * 3) + 2]
        );
    }

    if (gl_LocalInvocationID.x < m.vertex_count) {
        uint vertexIndex = VertexIndices[m.vertex_offset + gl_LocalInvocationID.x];

        vec4 locatiomyes = sceneInfo.camera_projection * sceneInfo.camera_view * model_matrix * vec4(Vertices[vertexIndex].position, 1.0);

        gl_MeshVerticesEXT[gl_LocalInvocationID.x].gl_Position = locatiomyes;

        uint mhash = hash(gl_WorkGroupID.x);
        vertexColor[gl_LocalInvocationID.x] = vec3(float(mhash & 255), float((mhash >> 8) & 255), float((mhash >> 16) & 255)) / 255.0;
    }
}