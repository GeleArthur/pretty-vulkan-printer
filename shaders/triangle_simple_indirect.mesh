#version 460
#pragma shader_stage(mesh)
#extension GL_EXT_mesh_shader: enable
#extension GL_EXT_shader_8bit_storage: enable

#include "shared_structs.glsl"
#include "world_binds.glsl"

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout (triangles) out;
layout (max_vertices = 64, max_primitives = 126) out;

layout (std430, set = 1, binding = 0) readonly buffer DrawCommandIn {
    DrawCommand Commands[];
};
layout (std430, set = 1, binding = 1) readonly buffer ModelMatrixIn {
    mat4 ModelMatrix[];
};
layout (std430, set = 1, binding = 2) readonly buffer MeshletIn {
    Meshlet Meshlets[];
};
layout (std140, set = 1, binding = 3) readonly buffer VertexIn {
    Vertex Vertices[];
};
layout (std430, set = 1, binding = 4) readonly buffer VertexIndicesIn {
    uint VertexIndices[];
};
layout (std430, set = 1, binding = 5) readonly buffer TriangleIndicesIn {
    uint8_t TriangleIndices[];
};
layout (std430, set = 1, binding = 6) readonly buffer SphereBoundsIn {
    ConeBounds SphereBounds[];
};

layout (location = 0) out vec3 vertexColor[];

taskPayloadSharedEXT Payload payload;

void main()
{
    uint meshletIndex = payload.MeshletIndices[gl_WorkGroupID.x];

    Meshlet m = Meshlets[meshletIndex];
    mat4 model_matrix = ModelMatrix[payload.model_matrix_id];

    if (gl_LocalInvocationIndex == 0)
    {
        SetMeshOutputsEXT(m.VertexCount, m.TriangleCount);
    }

    if (gl_LocalInvocationID.x < m.TriangleCount) {
        gl_PrimitiveTriangleIndicesEXT[gl_LocalInvocationID.x] = uvec3(
        TriangleIndices[m.TriangleOffset + (gl_LocalInvocationID.x * 3)],
        TriangleIndices[m.TriangleOffset + (gl_LocalInvocationID.x * 3) + 1],
        TriangleIndices[m.TriangleOffset + (gl_LocalInvocationID.x * 3) + 2]
        );
    }

    if (gl_LocalInvocationID.x < m.VertexCount) {
        uint vertexIndex = VertexIndices[m.VertexOffset + gl_LocalInvocationID.x];

        vec4 locatiomyes = sceneInfo.camera_projection * sceneInfo.camera_view * model_matrix * vec4(Vertices[vertexIndex].Position, 1.0);

        gl_MeshVerticesEXT[gl_LocalInvocationID.x].gl_Position = locatiomyes;

        uint mhash = hash(gl_WorkGroupID.x);
        vertexColor[gl_LocalInvocationID.x] = vec3(float(mhash & 255), float((mhash >> 8) & 255), float((mhash >> 16) & 255)) / 255.0;
    }
}