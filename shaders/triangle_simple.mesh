#version 460
#pragma shader_stage(mesh)
#extension GL_EXT_mesh_shader: enable
#extension GL_EXT_shader_8bit_storage: enable
#extension GL_EXT_buffer_reference: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types: require

#include "shared_structs.glsl"
#include "world_binds.glsl"

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout (triangles) out;
layout (max_vertices = 64, max_primitives = 126) out;


layout (std430, set = 1, binding = 0) readonly buffer MeshletIn {
    Meshlet mesh_lets[];
};
layout (std140, set = 1, binding = 1) readonly buffer VertexIn {
    Vertex vertices[];
};
layout (std430, set = 1, binding = 2) readonly buffer VertexIndicesIn {
    uint vertex_indices[];
};
layout (std430, set = 1, binding = 3) readonly buffer TriangleIndicesIn {
    uint8_t triangle_indices[];
};

layout (push_constant) uniform PushConstant {
    mat4x4 model;
    uint diffuse_texture_index;
    uint normal_texture_index;
    uint metalness_texture_index;
} pc;

layout (location = 0) out vec3 vertexColor[];

taskPayloadSharedEXT Payload payload;

void main()
{
    uint meshlet_index = payload.meshlet_indices[gl_WorkGroupID.x];

    Meshlet m = mesh_lets[meshlet_index];

    if (gl_LocalInvocationIndex == 0)
    {
        SetMeshOutputsEXT(m.vertex_count, m.triangle_count);
    }

    if (gl_LocalInvocationID.x < m.triangle_count) {
        gl_PrimitiveTriangleIndicesEXT[gl_LocalInvocationID.x] = uvec3(
        triangle_indices[m.triangle_offset + (gl_LocalInvocationID.x * 3)],
        triangle_indices[m.triangle_offset + (gl_LocalInvocationID.x * 3) + 1],
        triangle_indices[m.triangle_offset + (gl_LocalInvocationID.x * 3) + 2]
        );
    }

    if (gl_LocalInvocationID.x < m.vertex_count) {
        uint vertex_index = vertex_indices[m.vertex_offset + gl_LocalInvocationID.x];

        vec4 locatiomyes = sceneInfo.camera_projection * sceneInfo.camera_view * pc.model * vec4(vertices[vertex_index].position, 1.0);

        gl_MeshVerticesEXT[gl_LocalInvocationID.x].gl_Position = locatiomyes;

        uint mhash = hash(gl_WorkGroupID.x);
        vertexColor[gl_LocalInvocationID.x] = vec3(float(mhash & 255), float((mhash >> 8) & 255), float((mhash >> 16) & 255)) / 255.0;
    }
}